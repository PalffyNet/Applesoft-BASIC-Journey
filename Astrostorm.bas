10 REM ASTROSTORM BY PETER LEAR FROM 'COMPUTES! FIRST BOOK OF APPLE'. I DO NOT OWN THIS CODE. THIS IS FOR EDUCATIONAL PURPOSES. https://archive.org/details/Computes_First_Book_of_Apple/page/n5/mode/2up



20 FOR I = 770 TO 795: READ M: POKE I,M: NEXT I
25 DATA 172,01,03,174,01,03,169,04,32,168,252,173,48,192,232,208,253,136,208,239,206,0,03,208,231,96
30 GOTO 1000
100 TEXT : HOME : VTAB 1: PRINT "SCORE: ": POKE 34,2
110 FOR I = 1 TO 15: SP = INT (RND (1) * 39) + 2: VTAB 24: HTAB SP: PRINT "*": NEXT I
115 FOR I = 10 TO 50 STEP 40: POKE 768,10: POKE 769, I: CALL 770: NEXT I
119 OP = CP
120 POKE OP,160: VTAB 24: SP = INT (RND (1) * 39) + 2
130 HTAB (SP): PRINT "*"
140 IF PEEK (CP) = 170 THEN GOTO 800
145 POKE CP, CC: OP = CP
146 FOR I = 1 TO 25: NEXT
149 VTAB 1: HTAB 7: CALL - 868: VTAB 1: HTAB 7: PRINT PT
170 M = PEEK(-16384): IF M < 128 THEN GOTO 120
180 IF M - 128 = 8 THEN CC = 188:MV = - 1:GOTO 120
190 IF M - 128 = 21 THEN CC = 190:MV = + 1: GOTO 120
200 IF M - 128 <> 32 THEN GOTO 120
210 IF CP <> BP OR MV > 0 THEN GOTO 215
212 IF SF = 0 THEN GOTO 120
213 CP = BP + 39:SF = SF - 1: GOTO 230
215 CP = CP + MV 
220 IF CP <> BP + 39 THEN GOTO 230
222 IF SF = 6 THEN GOTO 600
225 CP = BP: SF = SF + 1
230 IF PEEK (CP) = 170 THEN GOTO 800
240 IF MV = 0 THEN PT = PT + INT((INT(DL * .2) * ((CP - BP) * SF)) / 2): GOTO 250
245 PT = PT + (MV * ( INT (DL * .3) * ((CP - BP) * SF)))
250 GOTO 120 
600 REM WIN
610 TEXT : HOME : VTAB 2: HTAB 15: PRINT "SCORE: "; PT
620 VTAB 8: HTAB 13: FLASH : PRINT "CONGRATULATIONS": NORMAL: VTAB 12: HTAB 14: PRINT "YOU MADE IT!"
640 POKE 768, 15: POKE 769, 50: CALL 770: POKE 768, 10: POKE 769, 10: CALL 770: POKE 768,15: POKE 769,50: CALL 770
650 FOR I = 1 TO 500: NEXT I
660 POKE -16368,0: GOTO 910
800 REM CRASH 
810 V = DL + 2:H = CP - BP
820 IF H <= 1 THEN VTAB V-1: HTAB H: PRINT "/": VTAB V: HTAB H: PRINT "-": VTAB V+1: HTAB H: PRINT " ";CHR$(92): GOTO 850
830 IF H >= 39 THEN VTAB V-1: HTAB H: PRINT CHR$(92): VTAB V: HTAB H: PRINT "-": VTAB V+1: HTAB H: PRINT "/": GOTO 850
840 VTAB V-1: HTAB H: PRINT CHR$(92);" /": VTAB V: HTAB H: PRINT "- -": VTAB V+1: HTAB H: PRINT "/ ";CHR$(92)
850 FOR I = 1 TO 200:X = PEEK ( - 16336): NEXT I 
890 POKE - 16368,0 
900 TEXT : HOME : VTAB 2: HTAB 15: PRINT "SCORE: "; PT
905 VTAB 10: HTAB 11: PRINT "YOU LOST YOUR SHIP!"
910 VTAB 21: HTAB 6: PRINT "SAME GAME ? Y)ES, N)O, E)ND ";: GET A$
915 IF A$ = "E" THEN END
920 IF A$ = "Y" OR A$ = CHR$(13) THEN GOTO 1130
1000 TEXT : HOME
1010 VTAB 5: HTAB 15: PRINT "ASTROSTORM"
1050 VTAB 10: HTAB 1: INPUT "ENTER DIFFICULTY LEVEL (5-18) ";DL$
1055 IF LEN (DL$) < 1 OR LEN (DL$) > 2 THEN GOTO 1050
1060 E = 0: FOR I = 1 TO LEN(DL$): CH$ = MID$(DL$,I,1): IF ASC(CH$) < 48 OR ASC(CH$) > 57 THEN E = 1
1070 NEXT I: ON E GOTO 1050
1080 DL = VAL (DL$)
1090 IF DL < 5 THEN VTAB 18: PRINT "TOO EASY": FOR I = 1 TO 1000: NEXT I: VTAB 18: CALL - 868: GOTO 1050
1100 IF DL > 18 THEN VTAB 18: PRINT "TOO HARD": FOR I = 1 TO 1000: NEXT I: VTAB 18: CALL -868: GOTO 1050
1110 VTAB 15: HTAB 10: INPUT "INSTRUCTIONS (Y/N)";A$: IF A$ = "Y" THEN GOTO 1200
1130 J = 0: FOR I = 1024 TO 1920 STEP 128
1140 J = J + 1
1150 IF J = DL + 2 THEN CP = I: I = 1921: GOTO 1180
1160 IF J + 8 = DL + 2 THEN CP = I + 40: I = 1921: GOTO 1180
1170 IF J + 16 = DL + 2 THEN CP = I + 80: I = 1921
1180 NEXT I 
1190 BP = CP:SF = 1:CC = 190:MV = + 1:PT = 0: GOTO 100
1200 TEXT : HOME 
1210 VTAB 3: HTAB 14: PRINT "INSTRUCTIONS"
1220 VTAB 6: PRINT "THE LEFT AND RIGHT ARROW KEYS CONTROL ": PRINT "THE DIRECTION OF MOVMENT OF THE ROCKET.";: PRINT "PRESS THE SPACE BAR TO MOVE THE ROCKET."
1230 PRINT : PRINT HTAB 4: PRINT "PRINT SPACE BAR TO BEGIN THE GAME"
1240 VTAB 20: HTAB 8: PRINT "PRESS ANY KEY WHEN READ": WAIT - 16384,128
1250 GOTO 1130 